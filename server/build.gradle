/*
 * The CroudTrip! application aims at revolutionizing the car-ride-sharing market with its easy,
 * user-friendly and highly automated way of organizing shared Trips. Copyright (C) 2015  Nazeeh Ammari,
 *  Philipp Eichhorn, Ricarda Hohn, Vanessa Lange, Alexander Popp, Frederik Simon, Michael Weber
 * This program is free software: you can redistribute it and/or modify  it under the terms of the GNU
 *  Affero General Public License as published by the Free Software Foundation, either version 3 of the
 *   License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without
 *  even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU Affero General Public License for more details.
 *  You should have received a copy of the GNU Affero General Public License along with this program.
 *    If not, see http://www.gnu.org/licenses/.
 */

apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'idea'

sourceCompatibility = 1.7
version = '1.0'

// run java "main" method
mainClassName = "org.croudtrip.app.CroudTripApplication"
run() {
    args 'server', 'configuration.yml'
}

// integration tests
def testSrcDir = file('src/integrationtest/java')
sourceSets {
    integrationTest {
        java.srcDir testSrcDir
        compileClasspath = sourceSets.main.output + configurations.testCompile + sourceSets.test.output
        runtimeClasspath = output + compileClasspath
    }
}
task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

// let intellij know about 'special' folders
idea {
    module {
        testSourceDirs += testSrcDir
    }
}

dependencies {
    compile 'io.dropwizard:dropwizard-core:0.8.0'
    compile 'io.dropwizard:dropwizard-hibernate:0.8.0'
    compile 'io.dropwizard:dropwizard-auth:0.8.0'
    compile 'io.dropwizard:dropwizard-forms:0.8.0'
    compile 'com.google.inject:guice:4.0-beta5'
    compile 'com.google.inject.extensions:guice-assistedinject:4.0-beta5'
    compile 'org.postgresql:postgresql:9.4-1201-jdbc41'
    compile 'be.fluid-it.tools.dropwizard:wizard-in-a-box:0.8-1-1'
    compile 'com.google.maps:google-maps-services:0.1.6'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile('com.squareup.retrofit:converter-jackson:1.9.0') {
        exclude group: 'com.fasterxml.jackson.core'
    }
    compile project(':models')
	compile fileTree(dir: 'libs', include: '*.jar')

    testCompile 'junit:junit:4.11'
    testCompile 'org.jmockit:jmockit:1.13'
    testCompile project(':client-retrofit')
}
