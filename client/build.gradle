/*
 * The CroudTrip! application aims at revolutionizing the car-ride-sharing market with its easy,
 * user-friendly and highly automated way of organizing shared Trips. Copyright (C) 2015  Nazeeh Ammari,
 *  Philipp Eichhorn, Ricarda Hohn, Vanessa Lange, Alexander Popp, Frederik Simon, Michael Weber
 * This program is free software: you can redistribute it and/or modify  it under the terms of the GNU
 *  Affero General Public License as published by the Free Software Foundation, either version 3 of the
 *   License, or (at your option) any later version.
 *  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without
 *  even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU Affero General Public License for more details.
 *  You should have received a copy of the GNU Affero General Public License along with this program.
 *    If not, see http://www.gnu.org/licenses/.
 */

import com.squareup.javapoet.ClassName
import de.bitdroid.jaxrs2retrofit.converter.IgnoreConverter
import io.dropwizard.auth.Auth;

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'de.bitdroid.jaxrs2retrofit'


buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'de.bitdroid.jaxrs2retrofit:plugin:0.3.0'
        classpath('io.dropwizard:dropwizard-auth:0.8.0') {
            exclude group: 'com.google.guava'
        }
        classpath 'com.android.tools.build:gradle:1.2.3'
        classpath 'io.fabric.tools:gradle:1.+'
    }
}


android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
    }

    defaultConfig {
        applicationId "org.croudtrip"
        minSdkVersion 16
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
		multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'LICENSE.txt'
    }

	lintOptions {
		ignore 'InvalidPackage'
	}

    // mark generated files as sources
    sourceSets {
        main {
            java {
                srcDir mainSourcesDir
                srcDir generatedSourcesDir
            }
        }
    }
}


project.tasks.withType(JavaCompile) { task ->
    options.compilerArgs << "-AguiceAnnotationDatabasePackageName=org.croudtrip"
}


dependencies {
    compile project(':models')
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile('com.squareup.retrofit:converter-jackson:1.9.0') {
        exclude group: 'com.fasterxml.jackson.core'
    }
    compile 'com.jakewharton.timber:timber:3.0.1'
    compile 'io.reactivex:rxjava:1.0.8'
    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.android.support:multidex:1.0.0'
    compile 'com.android.support:cardview-v7:21.0.+'
    compile 'com.android.support:recyclerview-v7:21.0.+'
    compile 'com.google.android.gms:play-services:7.0.0'
    compile 'com.google.android.gms:play-services-location:7.0.0'
    compile 'com.google.android.gms:play-services-maps:7.0.0'
    compile 'com.google.maps.android:android-maps-utils:0.3+'
    compile 'it.neokree:MaterialNavigationDrawer:1.3.3'
    compile 'com.getbase:floatingactionbutton:1.9.0'
    compile 'com.rengwuxian.materialedittext:library:2.1.3'
    compile 'org.roboguice:roboguice:3.0.1'
    provided 'org.roboguice:roboblender:3.0.1'
    compile 'pl.charmas.android:android-reactive-location:0.5@aar'
    compile 'com.larswerkman:HoloColorPicker:1.5'
    compile 'com.squareup.picasso:picasso:2.4.0'
    compile 'com.github.navasmdc:MaterialDesign:1.5@aar'
    compile('com.pnikosis:materialish-progress:1.5')
    compile('com.crashlytics.sdk.android:crashlytics:2.3.2@aar') {
        transitive = true;
    }
    androidTestCompile ('com.android.support.test:runner:0.3') {
        exclude group: 'com.android.support'
    }
    androidTestCompile ('com.android.support.test:rules:0.3') {
        exclude group: 'com.android.support'
    }
    androidTestCompile( 'com.android.support.test.espresso:espresso-core:2.2') {
        exclude group: 'com.android.support'
    }
}

// create retrofit interfaces from server resources
afterEvaluate {
    project.tasks.matching { it.name.startsWith('jaxRs2Retrofit') }.each {
        it.inputDir = file(project(':server').projectDir.toString() + '/' + mainSourcesDir)
        it.outputDir = file(generatedSourcesDir)
        it.packageName = 'org.croudtrip.api'
        it.paramConverterManager.registerConverter(ClassName.get(Auth.class), new IgnoreConverter())
        it.excludedClassNamesRegex = 'AvatarsResource|UsersHeadResource|LogsResource'
    }
}
